# Copyright (c) 2024 GOODIX.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Set firmware name
fw_img_name = "gr5515_sk_iotlink_demo"

use_legacy = false

if (use_legacy) {
  executable("${fw_img_name}.elf") {
    deps = [
      "ble_template:ble_example",
      "//build/lite:ohos",
    ]

    ldflags = [
      "-Wl,--whole-archive",
      "-lble_example",
      "-Wl,--no-whole-archive",
    ]
  }

  action("binary") {
    script = "//build/lite/run_shell_cmd.py"
    args = [
      "${compile_prefix}objcopy$toolchain_cmd_suffix",
      "-O",
      "binary",
      rebase_path("$root_build_dir/bin/$fw_img_name.elf"),
      rebase_path("$root_build_dir/bin/$fw_img_name.bin"),
    ]
    outputs = [ "$root_build_dir/bin/$fw_img_name.bin" ]
    deps = [ ":${fw_img_name}.elf" ]
  }

  action("hex") {
    script = "//build/lite/run_shell_cmd.py"
    args = [
      "${compile_prefix}objcopy$toolchain_cmd_suffix",
      "-O",
      "ihex",
      rebase_path("$root_build_dir/bin/$fw_img_name.elf"),
      rebase_path("$root_build_dir/bin/$fw_img_name.hex"),
    ]
    outputs = [ "$root_build_dir/bin/$fw_img_name.hex" ]
    deps = [ ":${fw_img_name}.elf" ]
  }

  action("disassembly") {
    script = "//build/lite/run_shell_cmd.py"
    args = [
      "${compile_prefix}objdump$toolchain_cmd_suffix",
      "-D",
      rebase_path("$root_build_dir/unstripped/bin/$fw_img_name.elf"),
      ">",
      rebase_path("$root_build_dir/bin/$fw_img_name.asm"),
    ]
    outputs = [ "$root_build_dir/bin/$fw_img_name.asm" ]
    deps = [ ":${fw_img_name}.elf" ]
  }

  group("gr5515_sk_iotlink_demo") {
    deps = [
      ":binary",
      ":hex",
      # ":disassembly",
    ]
  }
} else {
  import("//device/soc/goodix/gr551x/gr551x_executable.gni")

  gr551x_executable(fw_img_name) {
    gen_asm = false
    deps = [
      "ble_template:ble_example",
      "//build/lite:ohos",
    ]

    force_link_libs = [
      "ble_example",
    ]
  }
}